{//;Changelog
2018-12-10 - Should have been keeping a changelog this whole time. Oops.
2018-12-10 - Adjusted so bot DM warning for !afk in a pub chan even if notify=none
2019-01-14 - updated to use parsetime instead of doing it itself.
2019-01-18 - remove user from RS queus in #bso-rs. Only if _rs_auto_out=true
}

{suppresslookup} {//; suppresses errors from lookup system}
{set;~userid;{userid}}
{set;~afkchannel;{get;_afkChannel}}

{if;{argslength};<;1;
  __**Command Name:**__ {commandname}
  __**Usage:**__ !{commandname} <duration>

To indicate you have returned from AFK, type !back. For more information, type `!help afk`{return}
}

{set;~publicroles;{get;_guestRole};{get;_allyRole}}
{set;~rsrole;{get;_rsRole}}
{set;~everyoneCategory;{get;_publicCategory}}
{set;~ws-opponentChans;{get;_ws-chan-opp-main}}
{set;~limitedChans;{concat;{channels;{get;~everyoneCategory}};{get;~ws-opponentChans};{get;_rsChannels}}} {//; Ally-accessible channels}
{set;~mainwsTags;{get;_ws-tags-main}}
{set;~altwsTags;{get;_ws-tags-alts}}
{set;~wsrolemain;{get;_ws-role-main}}
{set;~maincorpNicks;{get;_corp-nick-main}}
{set;~altcorpNicks;{get;_corp-nick-alts}}
{set;~maincorpIDs;{get;_ws-role-main}}
{set;~altcorpIDs;{get;_ws-role-alts}}
{set;~wscmdschan;{get;_ws-chan-comm-main}}
{if;{userhasrole;{get;~publicroles}};{set;_{userid}_afk_notify;dm};{if;{length;{get;_{userid}_afk_notify}};==;0;{set;_{userid}_afk_notify;chan}}} {//; set default location for afk-messages}

{if;{indexof;{get;~limitedChans};{channelid}};>;-1;
	{if;{userhasrole;{get;~publicroles};{get;~userid}};==;false;
	  {delete}
      {set;~memberpublic;1}
    }
}

{set;~duration;{parseint;{execcc;parsetime;{args}}}}

{if;{get;~duration};==;0;Unable to parse your duration properly. Use `!help {commandname}` for usage information.{return}}

{if;{userhasrole;{get;~wsrolemain}};
  {if;{get;~duration};>;50400;
    Unable to set AFK duration beyond 14h while in a WS.{newline}Please report any extended absences to your ws-commander.
    {return}
  }
;
  {if;{get;~duration};>;172800;
    Unable to set AFK duration beyond 48h.{newline}{if;{userhasrole;{get;~publicroles}};;Please report any extended absences in <#{get;_statusChannel}>.}
    {return}
  }
}

{set;~futureunix;{math;+;{time;X};{get;~duration}}}
{set;~futureutc;{time;;{get;~futureunix};X}}
{//; The following is a workaround for time issue reported at cat town: https://discordapp.com/channels/194232473931087872/238131984017260546/468233337257852932}
{set;~tzparts;{split;{time;Z;;;{usertimezone;{get;~userid}}};:}}
{set;~tzdiff;{math;*;{math;+;{get;~tzparts;0};{math;/;{get;~tzparts;1};60}};3600}}
{set;~futurelocal;{time;;{math;+;{get;~futureunix};{get;~tzdiff}};X}}
{set;~time_in_words;{if;0{get;~days};{space}{get;~days}d}{if;0{get;~hours};{space}{get;~hours}h}{if;0{get;~mins};{space}{get;~mins}m}{if;0{get;~secs};{space}{get;~secs}s}}

{if;0{get;_{get;~userid}_afk_rspings};;
  {if;{userhasrole;{get;~rsrole};{get;~userid}};
    {void;{roleremove;{get;~rsrole};{get;~userid}}}
    {set;_{get;~userid}_has_rs;1}
  }
}

{set;~afkresponse;{usermention;{get;~userid}}, you are marked as AFK for the next{get;~time_in_words}. It's currently {if;0{get;~days};>;0;{time;MMMM Do,;;;{usertimezone;{get;~userid}}}{space}}{time;{if;0{get;_{get;~userid}_afk_24h};H:mm;h:mm a};;;{usertimezone;{get;~userid}}} local time and you are set to return {if;0{get;~days};>;0;on {time;MMMM Do;{get;~futurelocal}}{space}}at {time;{if;0{get;_{get;~userid}_afk_24h};H:mm;h:mm a};{get;~futurelocal}}.
{if;0{get;_{get;~userid}_afk};{newline}You previously marked yourself AFK but didn't use `!back` to indicate your return. Your AFK time has been updated.;{if;0{get;_{get;~userid}_afk_rspings};;{newline}Your access to #bso-rs been removed and will be restored when you come back.}}
{if;0{get;~memberpublic};{newline}**You just marked yourself AFK in a publically-visible or ally-visible channel!** Please be aware this is a bad idea while a WS is active.}}

{delete}

{switch;{get;_{get;~userid}_afk_notify};
  dm;
    {dm;{userid};{get;~afkresponse}}
  ;
  chan;
    {void;{send;{get;~afkchannel};{get;~afkresponse}}}
  ;
  none;
    {if;0{get;~memberpublic};{dm;{userid};{newline}**You just marked yourself AFK in a publically-visible or ally-visible channel!** Please be aware this is a bad idea while a WS is active.}}
  ;
}

{set;_{get;~userid}_afk;{get;~futureutc}}
{set;_{get;~userid}_afk_set;{time}}
{if;{indexof;{get;_afk_list};{get;~userid}};==;-1;{push;{get;_afk_list};{get;~userid}}}

{if;0{get;~memberpublic};==;0;{usernick} is set to AFK for{get;~time_in_words}.}
{//; RS auto-out: remove from all queues in #bso-rs}
{if;{get;_rs_auto_out};==;true;
	{set;bsors;{get;_rsChannel}}
	{set;~outMsg;{trim;{execcc;out;all {get;~userid} PASS {get;bsors}}}}
	{if;{length;{get;~outMsg}};>;0;{void;{send;{get;bsors};{get;~outMsg}}}}
	{if;{length;{get;~outMsg}};>;0;They have also been removed from all RS queues in #{channelname;{get;bsors}}.}
}
{//; end RS auto-out}
{if;{userhasrole;{get;~maincorpIDs}};
  {foreach;~role;{userroles;{get;~userid}};
    {set;~wsindex;{indexof;{get;~maincorpIDs};{get;~role}}}
    {if;{get;~wsindex};>;-1;
      {set;~corpchan;{get;~wscmdschan;{get;~wsindex}}}
      {if;0{get;_{get;~corpchan}_status_msg};{void;{delete;{get;~corpchan};{get;_{get;~corpchan}_status_msg}}}}
      {set;_{get;~corpchan}_status_msg;{send;{get;~corpchan};{execcc;wstimes;{get;~mainwsTags;{get;~wsindex}}}}}
      {set;_{get;~corpchan}_status_msg_time;{time}}
    }
  }
}

